# ========================================================
#
# GrammarMaster (.gm)
#
# ========================================================
#
# Define a general purpose grammar for your language and
# specify everything you need with a .gm file.
#
# Subtleties:
#   We leave deducing how the important bits work to the
#   reader! (It's intuitive, trust)
#
#   Two sections: 'SYMBOLICS' for lexing and 'STRUCTURE'
#   for the context free grammar rules.
#
#   'SYMBOLICS' works by alignment of the '->' symbol.
#       - This is defined by the line
#               <regex> -> <type> <value>
#         and only the position of the '->' matters.
#
#       - This means that indentation matters, in this
#         section only!
#
#       - All values are stripped, so if you want spaces
#         in your regex, figure it out.
#
#       - If no value is supplied, then the value of the
#         regex (if matched) is used for the token.
#
#       - A type of 'none' will not add the token during
#         lexing
#
#
#   'STRUCTURE' uses annotations (e.g '@action ...') to
#   define the reversal of CFG rules during parsing.
#       - The most recent annotation is used for each
#         CFG rule, so you don't have to keep writing
#         annotations all the time.
#
#       - Only '@action build' takes another argument
#         which is the name of the node you want to build.
#
#       - Currently '|' bars must be multiline only. Sorry
#         :(
#
#       - Indentation doesn't really matter, but do it
#         anyways.
#
# ========================================================
# ========================================================

SYMBOLICS
    <type>              ->  <regex>
    keyword fn          ->  fn
    keyword struct      ->  struct
    keyword if          ->  if
    keyword else        ->  [ \t\n]*else
    keyword while       ->  while
    keyword let         ->  let
    keyword return      ->  return
    keyword mod         ->  mod
    keyword create      ->  create
    keyword destroy     ->  destroy
    keyword var         ->  var
    keyword interface   ->  interface
    keyword implements  ->  implements
    keyword of          ->  of
    keyword as          ->  as
    keyword is          ->  is
    keyword embed       ->  embed
    keyword new         ->  new
    keyword mut         ->  mut
    keyword move        ->  move
    keyword trait       ->  trait
    keyword impl        ->  impl
    keyword Self        ->  Self
    keyword for         ->  for

    symbol @            ->  @
    symbol ,            ->  ,[ \t\n]*
    symbol {            ->  \s*{\s*
    symbol }            ->  [ \t\n]*}
    symbol [            ->  \[
    symbol ]            ->  \]
    symbol (            ->  \([ \t\n]*
    symbol )            ->  [ \t\n]*\)
    symbol []           ->  \[\s*\]
    symbol endl         ->  \n\s*

    operator =          ->  =
    operator :=         ->  :=
    operator <-         ->  <-
    operator +=         ->  \+=
    operator -=         ->  -=
    operator *=         ->  \*=
    operator /=         ->  /=
    operator ?          ->  \?
    operator ==         ->  ==
    operator !=         ->  !=
    operator +          ->  \+
    operator -          ->  -
    operator /          ->  /
    operator *          ->  \*
    operator and        ->  (&&|and)
    operator or         ->  (\|\||or)
    operator !          ->  (!|not)
    operator %          ->  %
    operator |          ->  \|
    operator <          ->  <
    operator >          ->  >
    operator <=         ->  <=
    operator >=         ->  >=
    operator .          ->  \.
    operator ::         ->  ::
    operator ->         ->  ->
    operator :          ->  :

    str                 ->  (["'])(?:(?=(\\?))\2.)*?\1
    int                 ->  -?[0-9]+
    bool                ->  true|false
    nil                 ->  nil

    TAG                 ->  [_a-zA-Z]\w*
    none                ->  [ \t]*
    none                ->  [ \t]*\/\/.*?\n+
    none                ->  [ \t]*\/\/[^\n]*


STRUCTURE
    # ====================================================
    # Aliases

    OP_ASSIGN ->
        @action pass
        |   =
        |   :=
        |   <-
        |   +=
        |   -=
        |   *=
        |   /=

    VEC ->
        @action filter_build new_vec
        |   TYPE []

    LITERAL ->
        @action pass
        |   int
        |   str
        |   bool
        |   flt
        |   nil
        |   []
        |   VEC

    VAR ->
        @action convert ref
        |   TAG

    ENDLS ->
        @action pass
        | endl
        | endl ENDLS

    # ====================================================
    # Binding
    BINDING ->
        @action filter_build void
        |   TAG
        @action filter_build new
        |   new TAG
        @action filter_build mut
        |   mut TAG
        @action filter_build var
        |   var TAG
        @action filter_build mut_var
        |   mut var TAG
        |   var mut TAG
        @action filter_build mut_new
        |   mut new TAG
        |   new mut TAG
        @action filter_build move
        |   move TAG

    BINDING_AND_TYPE ->
        @action filter_build void
        |   TYPE
        @action filter_build new
        |   new TYPE
        @action filter_build mut
        |   mut TYPE
        @action filter_build var
        |   var TYPE
        @action filter_build mut_var
        |   mut var TYPE
        |   var mut TYPE
        @action filter_build mut_new
        |   mut new TYPE
        |   new mut TYPE
        @action filter_build move
        |   move TYPE


    # ====================================================
    # Typing

    TYPE ->
        @action filter_build type
        |   TAG
        @action build type
        |   Self
        @action filter_build nilable_type
        |   TAG ?
        @action pass
        |   FN_TYPE
        @action filter_build para_type
        |   TAG [ TYPE ]
        |   TAG [ _TYPES ]

    BINDING_AND_TYPES ->
        @action filter_build prod_type
        |   _BINDING_AND_TYPES

    _BINDING_AND_TYPES ->
        @action pool
        |   BINDING_AND_TYPE , _BINDING_AND_TYPES
        |   BINDING_AND_TYPE , BINDING_AND_TYPE

    TYPES ->
        @action filter_build prod_type
        |   _TYPES

    _TYPES ->
        @action pool
        |   TYPE , _TYPES
        |   TYPE , TYPE

    FN_TYPE ->
        @action filter_build fn_type
        |   FN_TYPE_IN FN_TYPE_OUT

    FN_TYPE_IN ->
        @action filter_build fn_type_in
        |   ( BINDING_AND_TYPES )
        |   ( BINDING_AND_TYPE )
        |   ( )

    FN_TYPE_OUT ->
        @action filter_build fn_type_out
        |   -> BINDING_AND_TYPE
        |   -> ( BINDING_AND_TYPES )

    TYPING ->
        @action build_operator
        |   BINDINGS : TYPE
        |   BINDING : TYPE

    TEMPORARY_FIX1 ->
        @action filter_build prod_type
        |   TYPINGS

    TYPINGS ->
        @action pool
        |   TYPING , TYPING
        |   TYPING , TYPINGS


    # ====================================================
    # Expressions

    E12 ->
        @action build_operator
        |   E12 :: TAG
        |   TAG :: TAG

    E11 ->
        @action pass
        |   E12
        |   LITERAL
        |   VAR
        @action filter_pass
        |   ( E0 )

    E10 ->
        @action filter_build index
        |   E10 [ E0 ]
        @action pass
        |   E11

    E9 ->
        @action build_operator
        |   E9 . TAG
        @action build raw_call
        |   E9 PARAMS
        @action build curry_call
        |   E9 CURRIED_PARAMS
        @action filter_build cast
        |   E9 . as ( TYPE )
        @action pass
        |   E10

    E8 ->
        @action build_operator
        |   E8 * E9
        |   E8 / E9
        @action pass
        |   E9

    E7 ->
        @action build_operator
        |   E7 + E8
        |   E7 - E8
        @action pass
        |   E8

    E6 ->
        @action build_operator
        |   E6 < E7
        |   E6 <= E7
        |   E6 > E7
        |   E6 >= E7
        |   E6 == E7
        |   E6 != E7
        @action pass
        |   E7

    E5 ->
        @action handle_op_pref
        |   ! E5
        @action pass
        |   E6

    E4 ->
        @action build_operator
        |   E4 and E5
        |   E4 or E5
        @action pass
        |   E5

    E3 ->
        @action filter_build is
        |   E3 is TAG
        |   E3 is nil
        @action pass
        |   E4

    E0 ->
        @action pass
        |   E3


    # ====================================================
    # TUPLES

    E0 ->
        @action strip_annotation
        |   TAG : E0

    TAGS ->
        @action filter_build tags
        |   _TAGS

    BINDINGS ->
        @action filter_build bindings
        |   _BINDINGS

    CURRIED_PARAMS ->
        @action filter_build curried
        |   { E0 }
        |   { _E0S }
        |   { E0 ENDLS }
        |   { _E0S ENDLS }


    PARAMS ->
        @action filter_build params
        |   ( )
        |   ( E0 )
        |   ( _E0S )

        |   ! { E0 }
        |   ! { _E0S }
        |   ! { E0 ENDLS }
        |   ! { _E0S ENDLS }


    # tuple of tags
    _TAGS ->
        @action pool
        |   TAG , TAG
        |   TAG , _TAGS


    # tuple of Expressions
    E0S ->
        @action filter_build tuple
        |   ( _E0S )
        |   _E0S

    _E0S ->
        @action pool
        |   E0 , E0
        |   E0 , _E0S

    # tuple of bindings
    _BINDINGS ->
        @action pool
        |   BINDING , BINDING
        |   BINDING , _BINDINGS


    # ====================================================
    # LValues

    L2 ->
        @action pass
        |   VAR

    L1 ->
        @action build_operator
        |   L1 :: TAG
        @action pass
        |   L2

    L0 ->
        @action build_operator
        |   L0 . TAG
        @action pass
        |   L1

    L0S ->
        @action filter_build lvals
        |   _L0S

    _L0S ->
        @action pool
        |   L0 , L0
        |   L0 , _L0S


    # ====================================================
    # Assignment

    ASSIGNMENT ->
        @action build_operator
        |   L0 OP_ASSIGN E0
        |   L0S OP_ASSIGN E0S
        |   L0S OP_ASSIGN E0

    INFERENCE ->
        @action pool
        |   BINDINGS OP_ASSIGN E0S
        |   BINDINGS OP_ASSIGN E0
        |   BINDING OP_ASSIGN E0


    # ====================================================
    # DECL

    DECL ->
        @action convert_decl let
        |   let TYPING
        @action filter_build ilet
        |   let INFERENCE

    # ====================================================
    # ANNOTATION

    ANNOTATION ->
        @action filter_build annotation
        |   @ TAG ( _E0S )
        |   @ TAG ( E0 )
        |   @ TAG ( )

    # ====================================================
    # LINE

    LINE ->
        @action pass
        |   E0
        |   ASSIGNMENT
        |   DECL
        |   CONTROL
        |   ANNOTATION
        @action filter_build return
        |   return

    # ====================================================
    # Block

    SEQ ->
        @action filter_build seq
        |   { }
        |   { LINE }
        |   { LINES }
        |   { LINE ENDLS }
        |   { LINES ENDLS }

    LINES ->
        @action pool
        |   LINE ENDLS LINE
        |   LINE ENDLS LINES


    # ====================================================
    # Control flow

    COND ->
        @action filter_build cond
        |   ( E0 ) SEQ

    CONTROL ->
        @action filter_build if
        |   IF_CONTEXT
        @action filter_build while
        |   while COND

    IF_CONTEXT ->
        @action pool
        |   if COND
        |   if COND ELSES

    ELSES ->
        @action pool
        |   else SEQ
        |   else if COND
        |   else if COND ELSES


    # ====================================================
    # Function Definitions

    FUNC ->
        @action build_def
        |   fn TAG ARGS SEQ
        |   fn TAG ARGS RETS SEQ

    ARGS ->
        @action filter_build args
        |   ( )
        |   ( TYPING )
        |   ( TEMPORARY_FIX1 )

    RETS ->
        @action filter_build rets
        |   -> TYPING
        |   -> TEMPORARY_FIX1


    # ====================================================
    # Structs

    STRUCT ->
        @action filter_build struct
        |   struct TAG IMPLS { STRUCT_ELEMS }
        |   struct TAG { STRUCT_ELEMS }
        |   struct TAG { }

    IMPLS ->
        @action filter_build impls
        |   implements TAG
        |   implements TAGS

    STRUCT_ELEMS ->
        @action pool
        |   STRUCT_ELEM ENDLS STRUCT_ELEMS
        |   STRUCT_ELEM

    STRUCT_ELEM ->
        @action pool
        |   DECL
        |   TYPING
        |   FUNC
        @action filter_build create
        |   create ARGS CREATE_RETS SEQ
        @action filter_build destroy
        |   destroy ARGS SEQ
        @action filter_build embed
        |   embed TAG
        |   embed TAGS

    CREATE_RETS ->
        @action filter_build rets
        |   -> CREATE_TYPING

    CREATE_TYPING ->
        @action build_operator
        |   BINDING : TYPE


    # ====================================================
    # Traits

    TRAIT ->
        @action filter_build trait
        |   trait TAG { TRAIT_ELEMS }

    TRAIT_ELEMS ->
        @action pool
        |   TRAIT_ELEM ENDLS TRAIT_ELEMS
        |   TRAIT_ELEM

    TRAIT_ELEM ->
        @action pool
        |   TYPING

    TRAIT_DEF ->
        @action filter_build trait_def
        |   impl TAG for TAG { TRAIT_DEF_ELEMS }

    TRAIT_DEF_ELEMS ->
        @action pool
        |   TRAIT_DEF_ELEM ENDLS TRAIT_DEF_ELEMS
        |   TRAIT_DEF_ELEM

    TRAIT_DEF_ELEM ->
        @action pool
        |   FUNC


    # ====================================================
    # Interfaces

    INTERFACE ->
        @action filter_build interface
        |   interface TAG { INTERFACE_ELEMS }

    INTERFACE_ELEMS ->
        @action pool
        |   INTERFACE_ELEM ENDLS INTERFACE_ELEMS
        |   INTERFACE_ELEM

    INTERFACE_ELEM ->
        @action pool
        |   TYPING


    # ====================================================
    # Modules

    MOD ->
        @action filter_build mod
        |   mod TAG { CONTEXTS }
        |   mod TAG {  }

    CONTEXTS ->
        @action pool
        |   CONTEXT CONTEXTS
        |   CONTEXT

    CONTEXT ->
        @action pass
        |   FUNC
        |   STRUCT
        |   INTERFACE
        |   MOD
        |   ENDLS
        |   TRAIT
        |   TRAIT_DEF


    # ====================================================
    # Program

    START ->
        @action build start
        |   CONTEXTS
